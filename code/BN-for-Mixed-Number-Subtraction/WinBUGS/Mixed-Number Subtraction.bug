

model{

###############################################################
# Conditional probability of the observables
###############################################################

for (i in 1:n){
  for(j in 4:4){
    delta[i,j] <- pow(theta[i,1], Q[j,1])*pow(theta[i,2], Q[j,2])*pow(theta[i,3], Q[j,3])*pow(theta[i,4], Q[j,4])*pow(theta[i,5], Q[j,5])
    delta_plus_1[i,j] <- delta[i,j] + 1
		x[i,j] ~ dbern(pi_plus_1[delta_plus_1[i,j],j])
	}

  for(j in 6:12){
    delta[i,j] <- pow(theta[i,1], Q[j,1])*pow(theta[i,2], Q[j,2])*pow(theta[i,3], Q[j,3])*pow(theta[i,4], Q[j,4])*pow(theta[i,5], Q[j,5])
    delta_plus_1[i,j] <- delta[i,j] + 1
  	x[i,j] ~ dbern(pi_plus_1[delta_plus_1[i,j],j])
	}

for(j in 14:20){
    delta[i,j] <- pow(theta[i,1], Q[j,1])*pow(theta[i,2], Q[j,2])*pow(theta[i,3], Q[j,3])*pow(theta[i,4], Q[j,4])*pow(theta[i,5], Q[j,5])
    delta_plus_1[i,j] <- delta[i,j] + 1
  	x[i,j] ~ dbern(pi_plus_1[delta_plus_1[i,j],j])
	}



}


###############################################################
# Prior distribution for the latent variables
###############################################################
for(i in 1:n){
  theta[i,1] ~ dbern(gamma_1)

  theta_1_plus_1[i] <- theta[i,1] + 1
  theta[i,2] ~ dbern(gamma_2[theta_1_plus_1[i]])

  theta_1_plus_theta_2_plus_1[i] <- theta[i,1] + theta[i,2] + 1
  theta[i,5] ~ dbern(gamma_5[theta_1_plus_theta_2_plus_1[i]])

  theta_1_plus_theta_2_plus_theta_5_plus_1[i] <- theta[i,1] + theta[i,2] + theta[i,5] + 1
  theta_MN_plus_1[i] ~ dcat(gamma_MN[theta_1_plus_theta_2_plus_theta_5_plus_1[i], ])
  theta_MN[i] <- theta_MN_plus_1[i] - 1

  theta[i,3] <- step(theta_MN[i] - .5)

  theta[i,4] <- step(theta_MN[i] - 1.5)

}

###############################################################
# Prior distribution for the parameters
# that govern the distribution of the latent variables
###############################################################
gamma_1 ~ dbeta(21,6)

gamma_2[1] ~ dbeta(6,21)
gamma_2[2] ~ dbeta(21,6)

gamma_5[1] ~ dbeta(6,21)
gamma_5[2] ~ dbeta(13.5,13.5)
gamma_5[3] ~ dbeta(21,6)

gamma_MN[1,1:3] ~ ddirch(alpha_gamma_MN[1, ])  
alpha_gamma_MN[1,1] <- 16
alpha_gamma_MN[1,2] <- 8
alpha_gamma_MN[1,3] <- 6

gamma_MN[2,1:3] ~ ddirch(alpha_gamma_MN[2, ])  
alpha_gamma_MN[2,1] <- 12
alpha_gamma_MN[2,2] <- 10
alpha_gamma_MN[2,3] <- 8

gamma_MN[3,1:3] ~ ddirch(alpha_gamma_MN[3, ])  
alpha_gamma_MN[3,1] <- 8
alpha_gamma_MN[3,2] <- 10
alpha_gamma_MN[3,3] <- 12

gamma_MN[4,1:3] ~ ddirch(alpha_gamma_MN[4, ])  
alpha_gamma_MN[4,1] <- 6
alpha_gamma_MN[4,2] <- 8
alpha_gamma_MN[4,3] <- 16




###############################################################
# Prior distribution for the measurement model parameters
###############################################################
for(j in 1:J){
    pi_plus_1[1,j] ~ dbeta(3.5,23.5) 
    pi_plus_1[2,j] ~ dbeta(23.5,3.5) 
    pi_0[j] <- pi_plus_1[1,j]
    pi_1[j] <- pi_plus_1[2,j]

}


} # closes the model



